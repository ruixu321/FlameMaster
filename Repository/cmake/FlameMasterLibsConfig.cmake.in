# - Config file for the FlameMasterLibs package
# It defines the following variables:

#  FlameMasterLibs_INCLUDE_DIRS - include directories for FlameMasterLibs public headers
#  FlameMasterLibs_LIBS_DIRS    - location of the FlameMasterLibs (files if static libraries are used)
#  FlameMasterLibs_VERSION      - version of the library package

# Imported Targets:
#  FlameMasterLibs_LIBRARIES    - (all) libraries one might want to link against (consider specific libraries below)
#  FMLibs::Alligator            - library for memory allocation
#  FMLibs::ArrayMan             - library for (basic) linear algebra, vectors, matrices, and linear solver
#  FMLibs::FM                   - FlameMaster libraries (some mathematical operations, output postprocessing...)
#  FMLibs::Newton               - library for a newton solver
#  FMLibs::dassl                - library for (deprecated) Differential- Algebraic System Solver (DASSL)

@PACKAGE_INIT@
set(cfg_hint "The problem is most likely that the generation of FlameMasterLibsLibsConfig.cmake in lib/CMakeLists.txt is not up-to-date.")
set(FlameMasterLibs_VERSION "@FlameMasterLibs_VERSION@")
message("-- Found FlameMaster Libraries, Version ${@PROJECT_NAME@_VERSION}")
if(@set_check@)
	set_and_check(FlameMasterLibs_INCLUDE_DIRS "@PACKAGE_INSTALL_INCLUDE_DIR@")
	set_and_check(FlameMasterLibs_LIBS_DIRS "@PACKAGE_INSTALL_LIB_DIR@")
	set_and_check(FlameMasterLibs_ConfigPackageLocation "@PACKAGE_ConfigPackageLocation@")
	if (NOT PACKAGE_PREFIX_DIR STREQUAL "")
		message("-- Using 'FlameManLibs' from '${PACKAGE_PREFIX_DIR}'")
	endif()
else()
	# check include directory (should already exist, since build config will not be installed)
	FOREACH(directory ${FMLibs_src_header_direcotries})
		if(NOT EXISTS "${directory}")
	    	message(FATAL_ERROR "Directory from 'FMLibs_src_header_direcotries' not found: ${direcotry}")
		endif()
	ENDFOREACH()
	set(FlameMasterLibs_INCLUDE_DIRS "@FMLibs_src_header_direcotries@")
	# check again when find_package is used
	if(FlameMasterLibs_INCLUDE_DIRS STREQUAL "")
		message(FATAL_ERROR "FlameMasterLibs_INCLUDE_DIRS should contain source header directories, but it is empty."
							" ${cfg_hint}")
	endif()
	FOREACH(directory ${FlameMasterLibs_INCLUDE_DIRS})
		if(NOT EXISTS "${directory}")
	    	message(FATAL_ERROR "Directory from 'FlameMasterLibs_INCLUDE_DIRS' not found: ${direcotry}"
					"It seems like this build directory cannot find the source directory of the FlameMasterLibs anymore!"
					" Did you change the location of the build or the source tree after you executed cmake?"
					" ${cfg_hint}")
		endif()
	ENDFOREACH()
	# library directory (cannot be checked at configuration time)
	set(FlameMasterLibs_LIBS_DIRS "@FMLibs_build_lib_locations@")
	# check when find_package is used
	if(FlameMasterLibs_LIBS_DIRS STREQUAL "")
		message(FATAL_ERROR "FlameMasterLibs_LIBS_DIRS should contain library directories, but it is empty."
							" ${cfg_hint}")
	endif()
	FOREACH(file ${FlameMasterLibs_LIBS_DIRS})
		if(NOT EXISTS "${file}")
	    	message(FATAL_ERROR "Directory from 'FlameMasterLibs_LIBS_DIRS' not found: ${file}"
					"It seems like this build directory does not contain expected directories or files!"
					" ${cfg_hint}")
		endif()
	ENDFOREACH()
	set_and_check(FlameMasterLibs_ConfigPackageLocation "@PACKAGE_ConfigPackageLocation@")
	message("-- Using 'FlameManLibs' from a build tree")
	if (NOT FlameMasterLibs_LIBS_DIRS STREQUAL "")
		message("\t libraries:      '${FlameMasterLibs_LIBS_DIRS}'")
	endif()
	if (NOT FlameMasterLibs_INCLUDE_DIRS STREQUAL "")
		message("\t public headers: '${FlameMasterLibs_INCLUDE_DIRS}'")
	endif()
	if (NOT FlameMasterLibs_ConfigPackageLocation STREQUAL "")
		message("\t target import:  '${FlameMasterLibs_ConfigPackageLocation}/FlameMasterLibsTargets.cmake'")
	endif()
endif()

include(${FlameMasterLibs_ConfigPackageLocation}/FlameMasterLibsTargets.cmake)
set(FlameMasterLibs_LIBRARIES @FMnamespace@Alligator @FMnamespace@ArrayMan @FMnamespace@FM @FMnamespace@Newton)
if(@COMPILE_FORTRAN_SRC@)
	set(FlameMasterLibs_LIBRARIES ${FlameMasterLibs_LIBRARIES} @FMnamespace@Dassl)
endif(@COMPILE_FORTRAN_SRC@)

foreach(t ${FlameMasterLibs_LIBRARIES})
	if(NOT TARGET ${t})
		message(FATAL_ERROR "Expected target does not exist: '${t}'\n"
			" Note that targets are case sensitive."
			" ${cfg_hint}")
	endif()
endforeach()